# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
SUBSYSTEM_DIR = "../../../../.."
import("$SUBSYSTEM_DIR/core_service/telephony_core_service.gni")

rust_cxx("esim_ani_cxx_gen") {
  sources = [
    "src/wrapper.rs"
  ]
}

ohos_static_library("esim_ani_cxx") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "$SUBSYSTEM_DIR/core_service/frameworks/js/napi",
    "$SUBSYSTEM_DIR/core_service/interfaces/kits/native",
    "$SUBSYSTEM_DIR/core_service/interfaces/innerkits/include",
    "${target_gen_dir}/src",
  ]

  branch_protector_ret = "pac_ret"

  sources = [
    "src/cxx/ani_esim.cpp",
    "src/cxx/ani_esim_service_callback.cpp",
    "src/cxx/ani_reset_memory_callback.cpp",
  ]
  sources += get_target_outputs(":esim_ani_cxx_gen")

  deps = [
    ":esim_ani_cxx_gen",
    "$SUBSYSTEM_DIR/core_service/interfaces/innerkits:tel_core_service_api",
    "$SUBSYSTEM_DIR/core_service/utils:libtel_common",
  ]
  external_deps = [
    "ability_runtime:abilitykit_native",
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_single",
    "napi:ace_napi",
    "ffrt:libffrt",
    "rust_cxx:cxx_cppdeps",
  ]
  subsystem_name = "telephony"
  part_name = "core_service"
}

ohos_rust_shared_library("esim_ani") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    all_ubsan = true
    debug = false
  }

  sources = [ "src/lib.rs" ]
  
  external_deps = [
    "runtime_core:ani",
    "hilog:libhilog",
    "netmanager_base:ani_rs",
    "rust_cxx:lib"
  ]
  deps = [
    ":esim_ani_cxx",
    "//third_party/rust/crates/serde/serde:lib",
  ]

  clippy_lints = "none"

  subsystem_name = "telephony"
  part_name = "core_service"
  output_extension = "so"
}

generate_static_abc("esim_ani_abc") {
  base_url = "ets"
  files = [ "ets/@ohos.telephony.esim.ets" ]
  dst_file = "$target_out_dir/telephony_esim.abc"
  out_puts = [ "$target_out_dir/telephony_esim.abc" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/telephony_esim.abc"
}

ohos_prebuilt_etc("esim_ani_etc") {
  source = "$target_out_dir/telephony_esim.abc"
  module_install_dir = "framework"
  subsystem_name = "telephony"
  part_name = "core_service"
  deps = [ ":esim_ani_abc" ]
}

group("esim_ani_group") {
  if (core_service_support_esim) {
    deps = [
      ":esim_ani",
      ":esim_ani_etc",
    ]
  }
}