# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")
import("../../../core_service/telephony_core_service.gni")

SUBSYSTEM_DIR = "../../../"
TELEPHONY_CORE_SERVICE_ROOT = "$SUBSYSTEM_DIR/core_service"
TELEPHONY_INTERFACES_INNERKITS =
    "$TELEPHONY_CORE_SERVICE_ROOT/interfaces/innerkits"
TELEPHONY_FRAMEWORKS_NATIVE_ROOT =
    "$TELEPHONY_CORE_SERVICE_ROOT/frameworks/native/"
TELEPHONY_IMS_CORE_SERVICE_ROOT =
    "$TELEPHONY_CORE_SERVICE_ROOT/services/ims_service_interaction"

config("tel_core_service_api_config") {
  # header file path
  include_dirs = [
    "$TELEPHONY_INTERFACES_INNERKITS/include",
    "$TELEPHONY_INTERFACES_INNERKITS/ims/include",
    "$TELEPHONY_INTERFACES_INNERKITS/satellite",
  ]
  if (core_service_support_esim) {
    include_dirs += [ "${target_gen_dir}" ]
  }

  cflags = []
  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }
}

if (core_service_support_esim) {
  idl_interface_sources = [ "${target_gen_dir}/esim_service_proxy.cpp" ]

  idl_gen_interface("esim_service_api") {
    src_idl = rebase_path("IEsimService.idl")
    dst_file = string_join(",", idl_interface_sources)
    log_domainid = "0xD001F04"
    log_tag = "CoreServiceApi"
  }
}

ohos_shared_library("tel_core_service_api") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"
  sources = [
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/cell_information.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/cell_location.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/core_manager_inner.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/core_service_client.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/core_service_proxy.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/raw_parcel_callback_stub.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/i_network_search_callback_stub.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/ims_reg_info_callback_stub.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/network_information.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/network_search_result.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/network_state.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/nr_ssb_information.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/operator_config_types.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/resource_utils.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/signal_information.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/tel_ril_base_parcel.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/telephony_state_registry_client.cpp",
    "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/telephony_state_registry_proxy.cpp",
    "$TELEPHONY_IMS_CORE_SERVICE_ROOT/src/ims_core_service_callback_proxy.cpp",
  ]

  if (core_service_support_esim) {
    sources += [
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/download_profile_config_info_parcel.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/download_profile_result_parcel.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/downloadable_profile_parcel.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/esim_service_client.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/euicc_info_parcel.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/get_downloadable_profiles_result_parcel.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/iesim_service_callback_stub.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/profile_info_list_parcel.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/profile_metadata_result_parcel.cpp",
      "$TELEPHONY_FRAMEWORKS_NATIVE_ROOT/src/response_esim_result.cpp",
      "${target_gen_dir}/esim_service_proxy.cpp",
    ]
  }

  version_script =
      "$TELEPHONY_INTERFACES_INNERKITS/libtel_core_service_api.versionscript"

  defines = [
    "TELEPHONY_LOG_TAG = \"CoreServiceApi\"",
    "LOG_DOMAIN = 0xD001F04",
  ]

  defines += telephony_extra_defines

  public_configs = [ "$TELEPHONY_CORE_SERVICE_ROOT/utils:telephony_log_config" ]

  public_configs += [ ":tel_core_service_api_config" ]

  if (core_service_support_esim) {
    deps = [ ":esim_service_api" ]
  }

  external_deps = [
    "ability_base:want",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_single",
    "resource_management:global_resmgr",
    "samgr:samgr_proxy",
  ]

  if (core_service_support_esim) {
    external_deps += [ "ipc:ipc_core" ]
  }

  cflags_cc = [
    "-O2",
    "-D_FORTIFY_SOURCE=2",
  ]

  innerapi_tags = [ "platformsdk" ]
  part_name = "core_service"
  subsystem_name = "telephony"
}
